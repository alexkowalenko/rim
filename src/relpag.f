      SUBROUTINE RELPAG(THEROW)
      INCLUDE 'syspar.inc'
C
C  PURPOSE:   DO PAGING AS NEEDED FOR THE RELTBL RELATION
C
C  PARAMETERS:
C         THEROW--INPUT - ROW WANTED
C                 OUTPUT - ACTUAL ROW TO USE IN THE BUFFER
      INCLUDE 'reltbl.inc'
      INCLUDE 'rimcom.inc'
      INCLUDE 'f1com.inc'
      INTEGER THEROW
C
C  TURN THE REQUESTED ROW INTO A RECORD AND OFFSET.
C
      NNREC = ((THEROW - 1) / RPBUF) + 1
      NNROW = THEROW - ((NNREC - 1) * RPBUF)
C
C  SEE IF WE ALREADY HAVE THIS RECORD IN THE BUFFER.
C
      IF(NNREC.EQ.CRREC) GO TO 300
C
C  WE MUST DO PAGING.
C
C  SEE IF THE CURRENT RECORD IN THE BUFFER HAS BEEN MODIFIED.
C
      IF(RELMOD.EQ.0) GO TO 100
C
C  WRITE OUT THE CURRENT RECORD.
C
      CALL RIOOUT(FILE1,CRREC,RELBUF,LENBF1,IOS)
      IF(IOS.NE.0) RMSTAT = 2100 + IOS
C
C  READ IN THE NEEDED RECORD.
C
  100 CONTINUE
      RELMOD = 0
      CALL RIOIN(FILE1,NNREC,RELBUF,LENBF1,IOS)
      IF(IOS.EQ.0) GO TO 200
C
C  THERE WAS NO DATA ON THE FILE - WRITE SOME.
C
      CALL ZEROIT(RELBUF,LENBF1)
      CALL RIOOUT(FILE1,0,RELBUF,LENBF1,IOS)
      IF(IOS.NE.0) RMSTAT = 2100 + IOS
      LF1REC = LF1REC + 1
  200 CONTINUE
      CRREC = NNREC
C
C  SET THE POINTER TO THE ACTUAL ROW IN THE BUFFER.
C
  300 CONTINUE
      THEROW = NNROW
      RETURN
      END
